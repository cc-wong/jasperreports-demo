plugins {
	id 'org.springframework.boot' version '2.1.7.RELEASE'
	id 'java'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.asinc'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

ext {
	jasperReportsVersion = "6.9.0"
	itextVersion = "5.5.13.1"
	groovyVersion = "3.0.0-beta-3"
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven {
		// For dependency com.lowagie:itext:jar:2.1.7.js6, which is no longer at mavenCentral().
		// Ref: https://community.jaspersoft.com/questions/1071031/itext-js6-dependency-issue
		url 'http://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts/'
	}
}

configurations {
    jasperreports {
        transitive = true
    }
}

gradle.projectsEvaluated {
    processResources.dependsOn(compileJasperReports)
}

dependencies {
	implementation "org.springframework.boot:spring-boot-starter"
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// JAXB (package javax.xml.bind) no longer exists in JDK as of Java 11.
	implementation("javax.xml.bind:jaxb-api:2.3.1")

	// iText 5+ is used only during runtime, and so isn't needed for compile.
	// Refs:
	// https://www.reddit.com/r/java/comments/2x58or/how_to_add_jasperreports_library_to_gradle_project/
	// https://community.jaspersoft.com/questions/847229/jasperreports-6-itext-5-affero-licensing-catch
	compile("net.sf.jasperreports:jasperreports:${jasperReportsVersion}") {
		exclude group: 'com.itextpdf'
	}
	runtime("com.itextpdf:itextpdf:${itextVersion}")
	runtime("com.itextpdf:itext-pdfa:${itextVersion}")
	runtime("org.codehaus.groovy:groovy:${groovyVersion}")

	implementation("org.dom4j:dom4j:2.1.1")
	runtime("xalan:xalan:2.7.2") {
		// Exclude package javax.xml since it already exists in the JDK.
		// Java 11 does not allow the same package in multiple dependencies.
		exclude group: 'xml-apis'
	}

	// Required to generate outputs in MS Excel 97 format.
	// https://community.jaspersoft.com/questions/542132/xls-file-output-error
	runtime("org.apache.poi:poi:4.1.0")

	// Imports for task compileJasperReports.
	jasperreports "net.sf.jasperreports:jasperreports:${jasperReportsVersion}",
		"org.codehaus.groovy:groovy-all:${groovyVersion}"
}

/**
 * Compiles JasperReports templates at jr-templates/template.
 * Compiled .jasper files will be included with the compiled classes at report/template.
 */
task compileJasperReports(dependsOn: 'compileJava') {
    def jasperSourceDir = file('jr-templates/template')
    def jasperTargetDir = file("${sourceSets.main.output.resourcesDir}/report/template")
    println sourceSets.main.output.resourcesDir
    doLast {
	    ant {
	        taskdef(name: 'jrc', classname: 'net.sf.jasperreports.ant.JRAntCompileTask', classpath: configurations.jasperreports.asPath)
	        jasperTargetDir.mkdirs()
	        jrc(srcdir: jasperSourceDir, destdir: jasperTargetDir) {
	            classpath(path: sourceSets.main.output.classesDirs[0])
	            include(name: '**/*.jrxml')
	        }
	    }
    }
}
classes.dependsOn compileJasperReports
